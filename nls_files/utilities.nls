to-report get-one-with-largest-turn-angle [patch_set]

  report get-n-with-largest-turn-angle patch_set 1

end

to-report get-n-with-largest-turn-angle [patch_set n]

  let chosen-patch max-n-of n patch_set [ abs turn-amount ]
  
  report chosen-patch

end

to-report turn-amount  

  let this-patch self

  report [subtract-headings (towards this-patch) heading] of myself

end

to-report get-memory-fade-quotient

  report memory-fade-parameter / 4

end

to-report get-trail-fade-quotient

  report trail-memory-fade-parameter / 4

end

to memory-fade

  let fade-quotient get-memory-fade-quotient

  ask patches with [ pcolor >= memory-pcolor-min and pcolor <= memory-pcolor-max ]
  [
    set pcolor pcolor - fade-quotient

    if ( pcolor < memory-pcolor-min )
    [ set pcolor black ]
  ]

end

to trail-memory-fade

  let fade-quotient get-trail-fade-quotient

  ask patches with [ pcolor >= trail-pcolor-min and pcolor <= trail-pcolor-max ]
  [
    set pcolor pcolor - fade-quotient

    if (pcolor <= trail-pcolor-min)
    [ set pcolor black ]
  ]

end

to make-environment-corrections 
  
  ask patches with [ pycor = 0 or pxcor = 0 ] [set pcolor green]
  
end

to flush-record-distances
  
  let filename get-distance-normalizations-filename
  
  save-rows filename record-distances
  
  set record-distances [ ]
  
end

to flush-record-headings
  
  let filename get-heading-normalizations-filename
  
  save-rows filename record-headings
  
  set record-headings [ ]
  
end

to record-normalized-distance [ value ]
  
  ;; Since we want the BehaviorSpace experiments to run as fast as possible,
  ;; we will disable fill of dis-distances and dis-headings when experiments are running.
  ;; This is because these two lists tend to grow quite large 
  if ( fill-dis-arrays )
  [
    set dis-distances lput value dis-distances
  ]
  
  set record-distances lput ( list value ticks ) record-distances 
  
end

to record-normalized-heading [ value ]
  
  ;; Since we want the BehaviorSpace experiments to run as fast as possible,
  ;; we will disable fill of dis-distances and dis-headings when experiments are running.
  ;; This is because these two lists tend to grow quite large 
  if ( fill-dis-arrays )
  [
    set dis-headings lput value dis-headings
  ]
  
  set record-headings lput ( list value ticks ) record-headings
    
end

to record-current-position [ observed ]
  
  let cors ( list [ xcor ] of observed [ ycor ] of observed )
  
  let filename get-full-path-filename
  
  save-rows filename ( list cors )
  
end

to-report fill-dis-arrays
  
  ;; FROM THE DOCS:
  ;; Reports the current run number in the current BehaviorSpace experiment, starting at 1.
  ;; If no BehaviorSpace experiment is running, reports 0.
  report behaviorspace-run-number = 0
  
  
end
