globals [ file-name ]

extensions [ pathdir csv ]

to save-map
  
  let filepath user-new-file
  
  if ( is-string? filepath )
  [
    ifelse user-yes-or-no? (word "File will be saved at: " filepath
      "\nIf this file already exists, it will be overwritten.\nAre you sure you want to save?")
    [
      export-world filepath 
      user-message "File Saved."
    ]
    [ user-message "Save Canceled. File not saved." ]
  ]
  
end


to load-map
  
  let filepath user-file
  
  if ( is-string? filepath )
  [
    ifelse ( user-yes-or-no? (word "Load File: " filepath
      "\nThis will clear your current level and replace it with the level loaded."
      "\nAre you sure you want to Load?") )
    [
      import-world filepath
      
      set-file-name filepath
      
      user-message "File Loaded."
    ]
    [ user-message "Load Canceled. File not loaded." ]
  ]
  
  analysis-setup
  
end

to set-file-name [filepath]
  
  let reversed reverse filepath
  
  let sub substring reversed 0 ( position "/" reversed )
  
  let name reverse sub
  
  set file-name name
  
end

to choose-directory-and-fix-files-in-directory [ action ]
  
  let directory user-directory
  
  if ( not ( is-string? directory or directory ) )
  [
    stop
  ]
  
  fix-files-in-directory directory action
  
end

to fix-files-in-directory [ directory-path action ]
  
  let files filter [ first ? != "." ] pathdir:list directory-path
  
  foreach files [
    
    let full-file-path ( word directory-path ? )
    
    import-world full-file-path
    
    run action
    
    export-world full-file-path
  ]
  
end

to save-rows [ filename data ]
  
  if ( not is-results-directory-structure-ok? )
  [
    create-results-directory-structure
  ]
  
  if ( ticks = 0 and file-exists? filename )
  [
    file-delete filename
  ] 
    
  file-open filename
    
  foreach data [
    
    let row csv:to-row ?
      
    file-print row
  ]
  
  file-close 
  
end

to-report is-results-directory-structure-ok?
  
  report pathdir:isdirectory? "results" and 
  pathdir:isdirectory? "results/collisions" and
  pathdir:isdirectory? "results/headings" and
  pathdir:isdirectory? "results/distances" and
  pathdir:isdirectory? "results/paths" 
  
end

to create-results-directory-structure
  
  pathdir:create "results"
  pathdir:create "results/collisions" 
  pathdir:create "results/headings" 
  pathdir:create "results/distances" 
  pathdir:create "results/paths" 
  
end

to-report get-heading-normalizations-filename
  
  report get-filename ( word "results/headings/heading-normalizations" normalize-distance-with "-" activate-m-objects? ) 
  
end

to-report get-distance-normalizations-filename
  
  report get-filename ( word "results/distances/distance-normalizations" normalize-distance-with "-" activate-m-objects? ) 
  
end

to-report get-full-path-filename 
  
  report get-filename ( word "results/paths/path" normalize-distance-with "-" activate-m-objects? ) 
  
end

to-report get-collisions-filename
  
  report get-filename ( word "results/collisions/collisions" normalize-distance-with "-" activate-m-objects? ) 
  
end

to output-collisioned-with 
  
  if ( not ( pathdir:isdirectory? "results" and pathdir:isdirectory? "results/collisions" ) )
  [
    pathdir:create "results"
    pathdir:create "results/collisions"
  ]
  
  let filename get-collisions-filename
  
  save-rows filename collisioned-with
  
end

to-report get-filename [ filename-part ]
  
  report ( word filename-part behaviorspace-run-number ".csv" )
  
end  
